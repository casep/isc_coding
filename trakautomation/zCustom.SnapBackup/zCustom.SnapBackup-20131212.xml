<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="zCustom.SnapBackup">
<Super>%RegisteredObject</Super>
<TimeCreated>63088,57341.371786</TimeCreated>

<Method name="ReportStatus">
<Description>
Writes the status file with the specified contents</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>StatusFile:%String,Contents:%String,Level:%Integer</FormalSpec>
<Private>1</Private>
<Implementation><![CDATA[
	// write to cconsole.log
	do ##class(%SYS.System).WriteToConsoleLog(Contents,,Level)
	// file output
	set fs=##class(%File).%New(StatusFile)
	do fs.Open("WN")
	do fs.WriteLine($CASE(Level,0:"OK",1:"WARNING",2:"ERROR",3:"FATAL")_" - "_Contents)
	do fs.Close()
]]></Implementation>
</Method>

<Method name="Freeze">
<Description>
Freezes the Write Daemon, verifying and flagging success with a file</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>StatusFile:%String,TimeOut:%Integer</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set logprefix="##class("_##expression(""""_$get(%classname)_"""")_").Freeze - "
	do ##class(%SYS.System).WriteToConsoleLog(logprefix_"Called")
	if ##class(Backup.General).IsWDSuspended()
	{
		// we're running a freeze on an already-suspended instance
		do ..ReportStatus(StatusFile, logprefix_"Attempting to freeze an already suspended instance",1)
		quit $$$OK
	}
	else
	{
		// Run the Freeze
		if $DATA(TimeOut) && (TimeOut'="")
		{
			do ##class(%SYS.System).WriteToConsoleLog(logprefix_"Executing: ##class(Backup.General).ExternalFreeze(,,,,,,+"_TimeOut_")")
			set status=##class(Backup.General).ExternalFreeze(,,,,,,+TimeOut)
		}
		else
		{
			do ##class(%SYS.System).WriteToConsoleLog(logprefix_"Executing: ##class(Backup.General).ExternalFreeze()")
			set status=##class(Backup.General).ExternalFreeze()
		}
		// verify that it's frozen
		if ##class(Backup.General).IsWDSuspended()
		{
			if status
			{
				// all is happy
				do ..ReportStatus(StatusFile, logprefix_"##class(Backup.General).ExternalFreeze() Successful", 0)
				quit $$$OK
			}
			else
			{
				// we're frozen, but not entirely happy
				do $system.Status.DecomposeStatus(status,statusmessage)
				do ..ReportStatus(StatusFile, logprefix_"##class(Backup.General).ExternalFreeze() Failure: "_statusmessage,1, 1)
				quit $$$OK
			}
		}
		else
		{
			if stauts
			{
				// not suspended but we got good status
				do ..ReportStatus(StatusFile, "successful return but not frozen", 2)
				quit $$$ERROR($$$GeneralError,"successful return but not frozen")
			}
			else
			{
				// failed
				do $system.Status.DecomposeStatus(status,statusmessage)
				do ..ReportStatus(StatusFile, logprefix_"##class(Backup.General).ExternalFreeze() Failure: "_statusmessage, 2)
				quit $$$ERROR($$$GeneralError,statusmessage)
			}
		}
	}
]]></Implementation>
</Method>

<Method name="Thaw">
<Description>
Thaws the Write Daemon, verifying and flagging success with a file</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>StatusFile:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set logprefix="##class("_##expression(""""_$get(%classname)_"""")_").Thaw - "
	do ##class(%SYS.System).WriteToConsoleLog(logprefix_"Called")
	if ##class(Backup.General).IsWDSuspended()
	{
		// we are suspended - attempt thaw
		set status=##class(Backup.General).ExternalThaw()
		if status
		{
			// all is happy
			do ..ReportStatus(StatusFile, logprefix_"##class(Backup.General).ExternalThaw() Successful", 0)
			quit $$$OK
		}
		else
		{
			// something broke
			do $system.Status.DecomposeStatus(status,statusmessage)
			do ..ReportStatus(StatusFile, logprefix_"##class(Backup.General).ExternalThaw() Failure: "_statusmessage,2)
			quit $$$ERROR($$$GeneralError,statusmessage)
		}
	}
	else
	{
		// we're running a thaw on a not-suspended instance
		do ..ReportStatus(StatusFile, logprefix_"Attempting to thaw not suspended instance: Can't trust integrity of snapshots taken during Freeze",2)
		quit $$$OK
	}
]]></Implementation>
</Method>

<Method name="History">
<Description>
Sets the Backup History</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>StatusFile:%String,LogFile:%String=""</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set logprefix="##class("_##expression(""""_$get(%classname)_"""")_").History - "
	//do ##class(%SYS.System).WriteToConsoleLog(logprefix_"Called")
	set status=##class(Backup.General).ExternalSetHistory(LogFile,"##class("_##expression(""""_$get(%classname)_"""")_") based Backup Completed")
	if status
	{
		// all is happy
		do ..ReportStatus(StatusFile, logprefix_"##class(Backup.General).ExternalSetHistory() Successful", 0)
		quit $$$OK
	}
	else
	{
		// something broke
		do $system.Status.DecomposeStatus(status,statusmessage)
		do ..ReportStatus(StatusFile, logprefix_"##class(Backup.General).ExternalSetHistory() Failure: "_statusmessage,1)
		quit $$$ERROR($$$GeneralError,statusmessage)
	}
]]></Implementation>
</Method>

<Method name="JournalSwitch">
<Description>
Requests Journals be Switched (useful for regular journal backups)</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>StatusFile:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set logprefix="##class("_##expression(""""_$get(%classname)_"""")_").JournalSwitch - "
	//do ##class(%SYS.System).WriteToConsoleLog(logprefix_"Called")
	set status=##class(%SYS.Journal.System).SwitchFile()
	if status
	{
		// all is happy
		do ..ReportStatus(StatusFile, logprefix_"##class(Backup.General).ExternalSetHistory() Successful", 0)
		quit $$$OK
	}
	else
	{
		// something broke
		do $system.Status.DecomposeStatus(status,statusmessage)
		do ..ReportStatus(StatusFile, logprefix_"##class(%SYS.Journal.System).SwitchFile() Failure: "_statusmessage,1)
		quit $$$ERROR($$$GeneralError,statusmessage)
	}
]]></Implementation>
</Method>
</Class>
</Export>

